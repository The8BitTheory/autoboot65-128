#RetroDevStudio.MetaData.BASIC:8193,BASIC 65,uppercase,10,10
10 PRINT CHR$(27)"4"
20 PRINT USING "=######################################";"GOODWELL'S FAVORITE GAMES"
30 PRINT:PRINT
40 PRINT "A - ASSEMBLOIDS 2013"
50 PRINT "K - KAUFMANNSGILDE V2"
60 PRINT "S - STRAIGHT-UP"
70 PRINT
80 PRINT "ENTER YOUR CHOICE (X=EXIT)"
90 CURSOR 1,22
100 PRINT USING ">######################################";"BY THE 8-BIT THEORY"
110 PRINT
120 PRINT USING ">######################################";"PLEASE SUBSCRIBE";

130 GETKEY I$:IF I$="X" THEN END
140 IF I$="A" THEN FI$="ASSEMBLOIDS2013":GOTO 180
150 IF I$="K" THEN FI$="KAUFMANNSGILDEV2":GOTO 180
160 IF I$="S" THEN FI$="STRAIGHT-UP":GOTO 180
170 GOTO 130

180 CURSOR 5,10:PRINT "LOADING "FI$"!!!"

190 REM MARK $42000 AND COMPLETE BANK 5 AS USED
200 MEM 2,255

210 REM GET ROM VERSION (920398 IS REQUIRED FOR THE GETLFS FUNCTION)
220 V=FRE(-1)
230 IF V>=920398 THEN SYS $FF44:RREG,DU:ELSE DU=PEEK(186)

240 REM LOADS THE C64 RUN HELPER ROUTINE TO $42000
250 BLOAD"C64RUN",P($42000),R,U(DU)

260 REM PREPARE BANK 5 AND LOAD C64 PRG THERE
270 EDMA 3,0,255,$50000:BLOAD(FI$),P($50000),U(DU)

280 REM RUN C64RUN HELPER TO EXECUTE THE PRG IN C64 MODE
290 SYS $42000